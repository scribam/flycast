plugins {
    alias(libs.plugins.android.application)
}

def gitVersionName = providers.exec {
    commandLine("git", "describe", "--tags", "--always")
}.standardOutput.asText.get()

def getSentryUrl = { ->
	def url = System.env.SENTRY_UPLOAD_URL
	return url == null ? "" : url
}

android {
    namespace 'com.flycast.emulator'
    ndkVersion '29.0.14206865'
    compileSdk {
        version = release(36)
    }

    defaultConfig {
        applicationId "com.flycast.emulator"
        minSdk 21
        targetSdk 36
        versionCode 8
        versionName gitVersionName
        vectorDrawables.useSupportLibrary = true

        externalNativeBuild {
            cmake {
                arguments "-DANDROID_ARM_MODE=arm", "-DSENTRY_UPLOAD_URL=" + getSentryUrl(), "-DUSE_OPENMP=OFF", "-DANDROID_WEAK_API_DEFS=ON"
            }
        }

        ndk {
            moduleName 'flycast'
        }
        lintOptions {
            // avoid Error: Google Play requires that apps target API level X or higher.
            abortOnError false
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            storeFile file("../debug.keystore")
        }
        release {
            storeFile file("../playstore.jks")
            storePassword System.getenv("ANDROID_KEYSTORE_PASSWORD")
            keyAlias 'uploadkey'
            keyPassword System.getenv("ANDROID_KEYSTORE_PASSWORD")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    externalNativeBuild {
        cmake {
            path file('../../../CMakeLists.txt')
            version '3.22.1'
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES']
        }
        // not used
        jniLibs.excludes += 'lib/*/libz.so'
    }
    packaging {
        // This is necessary for libadrenotools custom driver loading
        jniLibs.useLegacyPackaging = true
    }
}

dependencies {
    implementation libs.appcompat
    implementation libs.commons.lang3
    implementation libs.httpclient5
    implementation libs.slf4j.android
    implementation fileTree(dir: 'libs', include: ['*.aar', '*.jar'], exclude: [])
    implementation libs.documentfile
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
}